{ Programm: Ein Schwimmbecken bauen
  Karol soll in der linken, hinteren Ecke stehen
  mit Blick nach Vorne = Süden }

{ wie Beispiel 06 jedoch zusätzlich mit den
  Anweisungen schnell und langsam }

// Anweisung Becken bauen
Anweisung BaueBecken
  schnell
  wiederhole 12 mal
    solange NichtIstWand tue
      Hinlegen
      Schritt
    *solange
    LinksDrehen
  *wiederhole
  langsam
*Anweisung

// Anweisung Becken abreissen
Anweisung AbbauenBecken
  schnell
  wiederhole 12 mal
    solange NichtIstWand tue
      Aufheben
      Schritt
    *solange
    RechtsDrehen
  *wiederhole
  langsam
*Anweisung

// Anweisung Becken durchschwimmen
Anweisung Schwimmen

  // lokale Anweisung
  Anweisung Umdrehen
    LinksDrehen
    LinksDrehen
  *Anweisung

  // Schwimmkörper bauen
  wiederhole 3 mal Hinlegen *wiederhole
  Schritt
  solange NichtIstZiegel tue
    wiederhole 3 mal Hinlegen *wiederhole
    Schritt
    Umdrehen
    wiederhole 3 mal Aufheben *wiederhole
    Umdrehen
  *solange
  Schritt
  Umdrehen
  wiederhole 3 mal Aufheben *wiederhole
  Umdrehen
*Anweisung

// Definition des Hauptteils
Anweisung Hauptteil

  BaueBecken

  { zur Mitte bewegen }
  LinksDrehen
  wiederhole 2 mal Schritt *wiederhole
  RechtsDrehen

  Schwimmen

  { zur Ecke bewegen }
  RechtsDrehen
  wiederhole 2 mal Schritt *wiederhole
  RechtsDrehen

  AbbauenBecken

  { zurück zur Ausgangsposition }
  solange NichtIstWand tue Schritt *solange
  LinksDrehen LinksDrehen
*Anweisung

{ ********************************************* }
{ *************** Programmanfang  ************* }
{ ********************************************* }
Programm
  wiederhole 4 mal
    Hauptteil
  *wiederhole
*Programm
{ *********** Programmende *********** }
